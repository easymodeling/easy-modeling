plugins {
    id 'java'
    id "org.sonarqube" version "3.3"
    id 'jacoco'
}

ext {
    projectGroup = 'io.github.easymodeling'
    projectVersion = '0.1.2'

    publicationPom = {
        name = 'Easy Modeling'
        description = 'A Java annotation processor that generates randomly populated objects for test use'
        url = 'https://github.com/easymodeling/easy-modeling'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                name = 'Zhe Zhang'
                email = 'easymodeling@outlook.de'
            }
        }
        scm {
            url = 'https://github.com/easymodeling/easy-modeling/'
        }
    }

    mavenCentral = {
        def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        url = project.version.endsWith("-SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
            username System.getenv("OSSRH_USERNAME") ?: ossrhUsername
            password System.getenv("OSSRH_PASSWORD") ?: ossrhPassword
        }
    }
}

group ext.projectGroup
version ext.projectVersion

subprojects {
    apply plugin: 'jacoco'

    group projectGroup
    version projectVersion

    repositories {
        mavenCentral()
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "easymodeling_easy-modeling"
        property "sonar.organization", "easymodeling"
        property "sonar.host.url", "https://sonarcloud.io"
        // ignore the integration-test project
        property "sonar.exclusions", "integration-test/**"
    }
}

test {
    useJUnitPlatform()
}
