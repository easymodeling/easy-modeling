plugins {
    id 'java'
    id "org.sonarqube" version "3.3"
    id 'jacoco'
}

ext {
    projectGroup = 'io.github.easymodeling'
    projectVersion = '0.0.1-SNAPSHOT'

    publicationPom = {
        name = 'Easy Modeling'
        description = 'A Java library that generates randomly populated models for test use'
        url = 'https://github.com/easymodeling/easy-modeling'
        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                name = 'Zhe Zhang'
                email = 'zhezhang@outlook.de'
            }
        }
        scm {
            url = 'https://github.com/easymodeling/easy-modeling/'
        }
    }

    releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    publicationCredentials = {
        username ossrhUsername
        password ossrhPassword
    }
}

group ext.projectGroup
version ext.projectVersion

subprojects {
    apply plugin: 'jacoco'

    group projectGroup
    version projectVersion

    repositories {
        mavenCentral()
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "zhezhangz_easy-modeling"
        property "sonar.organization", "zhezhangz"
        property "sonar.host.url", "https://sonarcloud.io"
        // ignore the example project
        property "sonar.exclusions", "example/**"
    }
}

test {
    useJUnitPlatform()
}
